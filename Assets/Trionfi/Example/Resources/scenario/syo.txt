;◎フリーマリーサンプル用ＡＳ
;
;『雪奈おねえさんのＭａｌｉｅシステム使用講座（初級編）』改
;
;
S:
&CG BLACK ;
&SOUND 2 ;
&CG_BG hikaesitu ;
@YUKINA_NIKKORI SEIFUKU C 10 ;
#雪奈 ”Ｍａｌｉｅ教室　ＩＮ　コレット”へようこそ〜〜！！

#1 へっ？　な、なんですか雪奈さん…

@YUKINA_NIKO ;
#雪奈 $1くん、今日はサンプルデータを使って、フリー版マリーｖｅｒ４．３の使い方を教えてあげる

#雪奈 とっても大事なことだから、よく聞いておいてね

#1 あれ、アナザーストーリーの作り方ならもう教わってますけど…？

@YUKINA_PUN 10 ;
#雪奈 もう、何言ってるの$1くん！

@YUKINA_IKARI 10 ;
#雪奈 フリーマリーでゲームを作る以上、新しい事も知っておかなくちゃいけないのっ。お客さまに色々訊かれるんだからね！

#1 え、そ、そうなの……？

#雪奈 Ｍａｌｉｅシステムを理解してゲームシナリオが作れないと、一人前のＭａｌｉｅ使いとは言えないのよっ！！

#01 （……そんな話聞いたことないけど、そうだったのか）



@YUKINA_NIKKORI ;
#雪奈 輝け！！　明日のシナリオライター！！

#雪奈 あなたもこれから、華麗なるゲームシナリオ書きに華麗な変身をとげるのよぉーーー！！

#雪奈 あの光の星に向かって、レッッツ、トラーーーイッ！！


&CG WHITE ;
#1 うおぉぉぉ！！！　よくわからんけど、やってやるぜーーー！！

&CG_BG HIKAESHITU ;
@YUKINA_SUMASI ;
#雪奈 じゃあ、さっそく講義を始めるわね


&FO ;

※まず、ゲーム画面をウィンドウモードに変更して下さい。

※［¥ｄａｔａ¥ｓｃｅｎａｒｉｏ］フォルダ内にある「ｓｙｏ．ｔｘｔ」を開いて、横に並べて下さい。

※このテキストを参照しながら、コマンドを覚えていって下さい

※それでは、Ａｒｅ　ｙｏｕ　ｒｅａｄｙ？






第一章　初級編　〜これさえ覚えればＭａｌｉｅシナリオは作れるっ！〜



;---------------
注！　どのコマンドの後でも必ず半角スペースを打ってからファイル名よ%HAATO
;---------------






１・メッセージの表示について


;セリフ

&CG_BG HIKAESITU ;
@YUKINA_SUMASI ;
#雪奈 まずは台詞の表示のさせ方からね

#雪奈 これはテキストを見て分かると思うんだけど、『＃○○　＊＊＊＊＊＊』という書式になっているの

（＃と名前の後のスペースは半角であることに注意してください）

#雪奈 たとえば、私のことを『綺麗なお姉ちゃん』と呼びたければ……

@YUKINA_NIKO ;
#綺麗なお姉ちゃん ……こうなるわけね

#01 （ふむふむ、なるほど。これは簡単だな……）




;発話者名表示

#雪奈 これで主人公の台詞もわかったわね？

（すべて半角で、『＃１スペース』の後に台詞を打ち込んで下さい）

（『＃１』が名前で、苗字は『＃２』になります）

#2 オレの名は$2$1。こういう感じか

（台詞などの中に主人公の名前を入れる場合、半角で『＄１』を使います）

（主人公名はコンフィグで変更してください）


;モノローグ

@YUKINA_SUMASI ;
#0雪奈 （いい調子ね……）

（＃の後に半角の０をはさむと、鍵カッコが外れてモノローグなどの形になります）

（この説明のような、ト書きに関しては、そのまま打ち込んで下さい）

#雪奈 半角文字を使いたい場合は、文字を”（ダブルクオーツ）で挟むの。"CORRETTO"みたいにね

@YUKINA_NIKKORI ;
#雪奈 あと、台詞やト書きの後には、必ず改行をはさんでおくのよっ☆

#1 はいっ、了解しました！



;手動改行

#雪奈 文章中に手動で改行を入れたい場合は、『＄Ｅ』を入れると、ゲーム画面で改行されて表示されるからね

#1 なるほど、試してみよう。$Eおっ、ちゃんと改行されてますね

@YUKINA_NIKO ;
#雪奈 でも、使いすぎに注意するのよ。$E文章が枠からはみ出たりすると、格好悪いから……

#雪奈 次は、背景とイベント絵の表示について説明するわねっ

&FO ;






２・背景とイベント絵の表示

&CG_BG HIKAESITU ;
@YUKINA_SUMASI ;
#雪奈 さっそくなんだけど…

#雪奈 外の様子を見てきてくれる？

#1 な、なんで……？

@YUKINA_SINKEN ;
#雪奈 これも説明のためなのっ！　いいから行きなさいっ！

オレは雪奈さんの怒気に押されるように……

&CG_BG MISEMAE ;
店の外へと出る。



#1 …………

#1 ……なるほど

（『＆ＣＧ＿ＢＧ』は６４０×４８０サイズの画像を表示するコマンドです。半角スペースの後に、表示したいファイル名を入力して下さい）

（画像ファイルは［¥ｄａｔａ¥ｐｉｃｔｕｒｅ¥ｂｇ］のフォルダに格納してください）

（使用できるファイル形式は、ＢＭＰ、ＰＮＧ、ＪＰＧがあります）


@MIZUHA_SUMASI SEIFUKU C ; 
#瑞葉 あっ、$1、暇ならお店の準備を手伝ってよ

#0雪奈 『$1くーーん、戻ってきてーー』

#1 はーーい

雪奈さんの声に、オレは控え室に戻っていく。

@MIZUHA_IKARI ;
#瑞葉 こらぁっ！　なに無視してんのよっ！！

&CG_BG HIKAESITU ;
#1 ……あれっ、雪奈さん……？

#雪奈 こっちよ

&FO ;
&CG Y_T_02A ;
ぽふっ。

#1 あ…………

#雪奈 イベントＣＧを表示させるのには、『＆ＣＧ』を使うのよ

#01 （む、ムネが……）


&CG Y_T_02B ;
#雪奈 わかった？

むぎゅっ。

雪奈さんは、さらに抱擁を強くする。

#雪奈 わかったって言いなさい

#1 むぐぐ……わかり……ました……

（『＆ＣＧ　ファイル名』でイベントＣＧが表示されます。半角スペースの後にＣＧの番号を入力して下さい）

&CG HIKAESITU CUT ;

（『＆ＣＧ』は、背景・イベントともに使うことができます。『＆ＣＧ＿ＢＧ』も同様です）

（また、『＆ＣＧ　ファイル名』の後に『ＣＵＴ』を付ける事によって、カット処理という瞬間表示が出来ます）

（画像ファイルは、［¥ｄａｔａ¥ｐｉｃｔｕｒｅ¥ｅｖｅｎｔ］のフォルダに格納してください）


@YUKINA_NIKO ;
#雪奈 次はキャラクターの表示方法ね

&FO ;



３・立ちキャラの表示について

&CG_BG HIKAESITU ;
@YUKINA_NIKO ;
#雪奈 ここまで来たらわかったと思うけど、立ちキャラの表示には＠を使うのよ


@MIZUHA_IKARI SEIFUKU R 100 ;
#瑞葉 ちょっと、ちょっとーー！！　二人してなにやってんのよーーーーー！！！

@YUKINA_SUMASI ;
#雪奈 そうそう、ちょうどこんな感じね

（＠記号の後に、『キャラ名＿表情　服装　立ち位置　時間』を入力して下さい）

（立ち位置には、Ｌ＝画面向かって左、Ｃ＝画面向かって中、Ｒ＝画面向かって右　…があるのよ%HAATO）

（立ちキャラ画像ファイルは、［¥ｄａｔａ¥ｐｉｃｔｕｒｅ¥ｃｈａｒａ］フォルダ内に服装ごとにフォルダを分けて格納してください）

$MIZUHA_SUMASI ;
#瑞葉 なに、もしかしてＡＳの説明？

（キャラクターの表示には、＄記号も使えます）

（同じように、『＄キャラ名＿表情　服装　立ち位置　時間』を入力して下さい）

（また、二回目からは服装と立ち位置を指定する必要はありません。時間の指定が無い場合は、３５０ミリ秒の速度で表示されます）

（立ち位置についての詳しい解説は後述します）

#雪奈 画面には最大６人まで表示できるから、よく考えて立たせることね



@YUKINA_NIKO SIFUKU ;私服（部屋着）

#雪奈 着替えたい時はこんなふうに服装のフォルダ名を指定するのよ


#雪奈 それから……えいっ！

&CHARCLEAR ;
#瑞葉 きゃっっ！！！

（キャラクターを退場させる時は、『＆ＣＨＡＲＣＬＥＡＲ』を使います）


&CHAR MIZUHA MAKKA SEIFUKU R ;
#瑞葉 もう〜急にびっくりするじゃない

（立ちキャラクターを表示させるのには、『＆ＣＨＡＲ』という書式も使えます）

（この場合も同じように、『＆ＣＨＡＲ　名前　表情　服装　立ち位置　時間』の順で打ち込んでください。名前と表情の間の＿は要りません）

@YUKINA_NIKO L ;
#雪奈 じゃあ瑞葉ちゃん、もうちょっとしたら私たちもお店に出るから、先に行っててくれる？

$MIZUHA_SUMASI ;
#瑞葉 う、うん……

&CHARCLEAR MIZUHA ;

（特定のキャラクターを退場させたいときは、『＆ＣＨＡＲＣＬＥＡＲ　キャラクター名』と記述してください）


#1 なるほど…

@YUKINA_SUMASI C ;
#雪奈 次はよく使われる小技——演出について説明するわよ

&FO ;









４・演出効果について


&CG_BG HIKAESITU ;
@YUKINA_SUMASI SEIFUKU C ;
#雪奈 えっとね……

#雪奈 …………

&WAIT 10 ;
@YUKINA_NIKO ;
#雪奈 今みたいに待ち時間をおきたいときは、『＆ＷＡＩＴ』を使うの

（『＆ＷＡＩＴ　時間』で次のコマンドまでウェイトをかけます）

（数値は１ごとに３００ミリ秒です）

#雪奈 もっと細かくウェイトをかけたい人には、『＆ＷＡＩＴＭＳ』というのもあって…

@YUKINA_SUMASI ;
#雪奈 これは数値が１で１０００分の１秒……１ミリ秒になるのよ

&WAITMS 500 ;

（今ので０．５秒のウェイトをかけました）


#雪奈 それから…$K『＄Ｋ』でクリック待ちをかけることができるの



;ハートマーク表示
@YUKINA_NIKKORI ;
#雪奈 記号としてハートマークを表示させるのには、文章中に『％００』を打つのよ。もちろん半角でね%00

#1 そうだったんだ〜〜%00

@YUKINA_SUMASI ;
#雪奈 ハートマークに限って％ＨＡＡＴＯでも指定が出来るので、使ってみてね%HAATO

#1 無視しなくてもいいじゃん%07

（％０７は怒りマークです）




&FO ;フェードアウト
#1 うおーーっ、暗いぞーーっ！！

#雪奈 もう何度か出てきてるけど、『＆ＦＯ』で画面を暗くさせるのよ



&FLASH ;フラッシュ
#雪奈 あとは、『＆ＦＬＡＳＨ』で画面を光らせたり…



&CG_BG HIKAESHITU ;

&SHAKE ;揺らし

#雪奈 『＆ＳＨＡＫＥ』で揺らしてみたり




&BREAK ;ブレーク

#雪奈 『＆ＢＲＥＡＫ』で画面を崩しながらフェードアウトさせてみたり…





&CG_BG HIKAESHITU ;

&SHADEFO ;シェードフェードアウト

#雪奈 『＆ＳＨＡＤＥＦＯ』では画面をぼかしながらフェードアウトさせたり…





&FO WO ;ホワイトアウト

#雪奈 『＆ＦＯ　ＷＯ』又は『＆ＦＯ　ＷＨＩＴＥ』では、画面の左上から円形に白く広がるのよ

#1 なるほど、色んな効果があるんですね




&CG_BG HIKAESHITU ;
@YUKINA_SUMASI ;
#雪奈 それと、コマンドの後ろにある”；”（セミコロン）は、これより後ろに書かれている文章を無効にするっていう意味があるの。注意書きなんかに使うといいわね

@YUKINA_NIKO ;
#雪奈 とりあえず今日はこんなトコかしら。それじゃ、私たちも仕事に行きましょう

#1 はいっ

&BREAK ;
&SOUNDSTOP FO ;
&WAIT 10 ;

——そして午後。

&SOUND 2 ;
&CG_BG TENNNAI ;
#1 ふう、もうすぐ３時か……

#01 （なんかまだ重要な事を教わってないような気がするが……なんだっけな……）

@MIZUHA_NIKO SEIFUKU C ;
#瑞葉 $1、もうすぐ休憩でしょっ。私が代わってあげるから、お昼ご飯食べて来ていいよ

#1 おお、そうさせてもらうよ。ああ、でも…

@MIZUHA_SUMASI ;
#瑞葉 ……どーしたの？

#1 ほら、もうすぐランチタイムサービスも終わりだろ。店内のＢＧＭを変更しようかと思ったんだが、やり方が分からなくてさ……

@MIZUHA_NIKKORI ;
#瑞葉 そんなの簡単っ！　『＆ＳＯＵＮＤ』を使えばいいのよ！

#1 そうか、それだっ！

&FO ;




５・音楽と効果音について

（ＢＧＭを再生するのには、『＆ＳＯＵＮＤ　ファイル番号』という書式を使います）

&CG TENNNAI ;
&SOUND 10 ;
@MIZUHA_NIKKORI ;
#瑞葉 ほらっ、音楽が変わったでしょ

#瑞葉 音楽用のファイルは、［¥ｄａｔａ¥ｂｇｍ］のフォルダに入れてね

&SOUNDSTOP ;
@MIZUHA_SUMASI ;
#瑞葉 音楽を止める時は『＆ＳＯＵＮＤＳＴＯＰ』で、ゆっくり止めたい時は、『ＦＯ』を後ろにくっつけると可能よ

#1 ゆっくり止める時は『ＦＯ』か……

&SE 01 ;ドンガラ
#雪奈 あ〜〜〜〜〜！！

#雪奈 効果音を再生するには『＆ＳＥ　ファイル名』なの〜〜

&SOUND 2 ;
（ＢＧＭ・効果音とも、使用できるファイル形式についてはマニュアルを参照してください）

（ＢＧＭファイルは番号で、効果音ファイルはファイル名で管理します）

#1 なんだ、厨房から聞こえてきたぞ

#雪奈 $1くん、ごめんね〜〜

@MIZUHA_UTUMUKI ;
#瑞葉 また雪奈ねえったらお皿割ったわね

#1 いや、いいじゃないか。おかげで効果音の使い方も分かったし

@MIZUHA_IKARI ;
#瑞葉 良くないわよっ！

&CHARCLEAR MIZUHA ;
#0瑞葉 『また雪奈ねえはお皿割って！　もう仕事終わりにして休んでなさい！！』

#0雪奈 『うううう〜〜〜〜〜』

&SOUNDSTOP FO ;
#1 所でオレの休憩は？

&FO ;
&WAIT 6 ;
『＆ＧＯＴＯＴＥＸＴ　』命令を使用してサンプルシナリオ冒頭に戻ります。終了する場合は、ＱＵＩＴを選んでください。

&GOTOTEXT SCENARIO ;
&END ;

