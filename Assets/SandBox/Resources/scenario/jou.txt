;
;
;『瑞葉ちゃんのＭａｌｉｅシステム使用講座（上級編）』改
;
;
;
&CG BLACK ;
&SOUND 2 ;
&CG_BG HIKAESITU ;
@YUKINA_NIKO SEIFUKU L ;
#雪奈 $1くん、今日は一緒に選択肢の勉強をしましょう


#1 またなんだか唐突ですね

@MIZUHA_SUMASI SEIFUKU R ;
#瑞葉 どうする？　やるの、やらないの？

&JUMP ("勉強しまっせ",A1,"また今度にして",A2) ;





A1:
#1 おっけーーっ！　よしっ、さっそく勉強を始めましょう

@YUKINA_NIKKORI ;
#雪奈 そうこなくっちゃ

&GOTO B_1 ;




A2:
@MIZUHA_PUN ;
#瑞葉 えーーっ！　それはないんじゃなーい？

@YUKINA_UTUMUKI ;
#雪奈 残念だわーー

#1 すいません。今日はちょっと忙しくて……


&FO ;
&GOTO END ;







B_1:
&FO ;
&WAIT 5 ;
第三章　上級編　$E〜分岐とパラメーター・これさえあればゲームが作れるかも〜




※まず、ゲーム画面をウィンドウモードに変更して下さい。

※『フリーマリーｖｅｒ４』のインストールフォルダ内の［¥ｄａｔａ¥ｓｃｅｎａｒｉｏ］フォルダ内にある［ｊｏｕ］というファイルを開いて、横に並べて下さい。

※このテキストを参照しながら、コマンドを覚えていって下さい

※それでは、Ａｒｅ　ｙｏｕ　ｒｅａｄｙ？








１・選択肢とラベルによる分岐


&CG_BG HIKAESITU ;
@YUKINA_SUMASI ;
@MIZUHA_SUMASI ;
#1 さっきから気になっていたんだが、<FC YELLOW>『Ａ２：』</FC>とか<FC YELLOW>『Ｂ＿１：』</FC>ってのは何だい？

@YUKINA_NIKO ;
#雪奈 これはラベルといって、分岐を発生させたときの進み先を表しているのよ。目印みたいなものね

（ラベルの後にはかならず『：』（コロン）を付けてください）

#1 よしっ、分かったぞ。次行こう、次！

@MIZUHA_UTUMUKI ;
#瑞葉 ホントに分かってんの？

#1 …で、さっきみたいな選択肢はどうやるんだ？


&CHARCLEAR ;
（選択肢を表示させるのには、<FC YELLOW>『＆ＪＵＭＰ　』</FC>を使います）

（書式は半角で$E<FC YELLOW>『＆ＪＵＭＰ　（”いろ”，Ａ，”はに”，Ｂ）』</FC>になります）

（「いろ」を選ぶとラベルＡに、「はに」を選ぶとラベルＢに、それぞれ飛ぶことになります）

&JUMP ("秘拳、バッファロー！",B1,"なめなめ攻撃",B2) ;




B1:
#1 とりゃぁぁーーー！！！　バッファローーーー！！！！

ぼよよ〜〜〜ん♪

@MIZUHA_MAKKA ;
#瑞葉 きゃぁぁああああああああ！！！

&GOTO C_1 ;


B2:
#1 なめなめなめなめなめなめなめ…………

@MIZUHA_MAKKA ;
#瑞葉 いやあぁぁぁ！！！

&GOTO C_1 ;



C_1:
#1 なるほど、こういう感じか…

@MIZUHA_SUMASI ;
#瑞葉 これで選択肢の使い方はばっちりね！

@YUKINA_NIKO ;
#雪奈 選択肢の項目は、最大で１０個まで使用できるからね

（１１個目からは画面に項目が表示されませんので、気を付けて下さい）





#1 で、<FC YELLOW>『＆ＧＯＴＯ　』</FC>というのがこのラベルへ行けっていう命令か

@MIZUHA_NIKO ;
#瑞葉 まあ、そのまんまの命令ね。いわゆる共通部分に戻ったりするときは、必ず付けること

&CHARCLEAR ;
（<FC YELLOW>『＆ＧＯＴＯ　ラベル名』</FC>で、そのラベルに直行します）

（また、<FC GREEN>『＆ＧＯＴＯＴＥＸＴ　シナリオファイル名』</FC>で、特定のテキストファイルへ直行します。ファイルは必ず初期シナリオと同じフォルダ内に格納してください）

（<FC RED>※注</FC>　テキストのファイル名には全角文字、及び数字が頭に来ているものは使えません！！）

@MIZUHA_SUMASI ;
#瑞葉 じゃあ、次は『パラメーター』について説明するわね

&FO ;








２・パラメーターコマンドで好感度アップっ！！

&CG HIKAESITU ;
（パラメーターを使用するには、<FC YELLOW>『＆ＰＡＲＡＭＥＴＥＲ　』</FC>という書式を用います）

（これは数値を記録しておく時に使うコマンドです）

@MIZUHA_NIKO ;
#瑞葉 ＡＤＶゲームに付き物の好感度とかいうのはこれを使うのよ

（書式は<FC YELLOW>『＆ＰＡＲＡＭＥＴＥＲ　<FC RED>Ｘ　＋　１</FC>』</FC>のように記述します）

（パラメーターコマンドを使うときは、最初にパラメーターの初期化を行って下さい）

&PARAMETER DOKI = 0 ;
（これで、パラメーター「ＤＯＫＩ」が数値０で設定されました）


@MIZUHA_NIKKORI ;
#瑞葉 『＝』は代入を表すの。パラメーターコマンドで使える演算子は、『＋』は足し算、『−』で引き算ね

（他には、『＊』で掛け算、『％』で割り算が出来ます）


@YUKINA_NIKKORI L ;
#雪奈 それでは、さっそく選択肢によるパラメーター変化を試してみましょう

&JUMP ("瑞葉を見つめる",C1,"瑞葉の手を握る",C2,"瑞葉を押し倒す",C3)




C1:
じーーーーー。

@MIZUHA_MAKKA ;
#瑞葉 …な、なによ…… 

&PARAMETER DOKI + 1 ;
（これでパラメーター「ＤＯＫＩ」が　＋１　されました）

&GOTO D_1 ;


C2:
にぎっ。

@MIZUHA_MAKKA ;
#瑞葉 きゃっ！

&PARAMETER DOKI + 2 ;
（これでパラメーター「ＤＯＫＩ」が　＋２　されました）

&GOTO D_1 ;


C3:
@MIZUHA_SINKEN ;
&SE 30 ;
&FLASH ;
&SHAKE ;

#1 ギャース！

@MIZUHA_IKARI ;
#瑞葉 なにすんのよっ！

（パラメーター「ＤＯＫＩ」は変化しませんでした）

&GOTO D_1 ;






D_1:

@YUKINA_NIKKORI2 ;
#雪奈 今度は、変化したパラメーターをもとに、お話を分岐させてみましょうね

&CHARCLEAR ;




３・条件式で、結末を変化させよう！！

@YUKINA_NIKO ;
#雪奈 条件式を使ったパラメーター分岐を発生させるのには、$e<FC YELLOW>『＆ＩＦ〜ＴＨＥＮ〜』</FC>というコマンドを使います

#1 ＩＦ……もしも、というやつか。自慢じゃないが、模試の成績は悪かったぞ

（書式は<FC YELLOW>『＆ＩＦ　条件式　ＴＨＥＮ　命令　』</FC>となります）

@YUKINA_NIKKORI2 ;
#雪奈 条件式で使用できる演算子には、『＝＝』（等号）、『＜』（右より小さい）、『＞』（右より大きい）、『＜＝』（以下）、『＞＝』（以上）、『！＝』（不等号）などがあるの

@MIZUHA_SUMASI ;
#瑞葉 条件式に、その条件がピッタリ合致すれば、ＴＨＥＮの後の命令が実行されるってこと

#1 ふむ……

@MIZUHA_NIKO ;
#瑞葉 ま、使ってみた方が早いでしょ

&IF DOKI==0 THEN @MIZUHA_IKARI ;
（ＤＯＫＩ　が０であれば、瑞葉の表情が怒りに変化します）

&IF DOKI!=0 THEN @MIZUHA_NIKKORI2 ;
（それ以外であれば、笑顔に変わります）

@YUKINA_NIKO ;
#雪奈 こういう感じよ

#1 うーん、なるほど……

&CHARCLEAR ;













４・条件付きジャンプコマンドについて

@YUKINA_SUMASI L ;
#雪奈 次は<FC YELLOW>『＆ＣＪＵＭＰ　』</FC>ね。これは条件付きジャンプコマンドといって、パラメーターの数値によって、選択肢の項目を増やしたりできるのよ

（書式は、$E<FC YELLOW>『＆ＣＪＵＭＰ　（”ほへ”，Ｃ，条件式１，”とち”，Ｄ，条件式２）』</FC>と記述します）

（『ほへ』は条件式１を満たしている場合のみ、『とち』は条件式２を満たしている場合のみ表示されます）

（必ず表示させたい項目には、条件式を<FC RED>１</FC>に設定して下さい）

（応用で<FC GREEN>『＆ＣＪＵＭＰＴＥＸＴ　』</FC>というコマンドも使えます）

#1 うむむむむ……

@MIZUHA_NIKO R ;
#瑞葉 まあ、これも使ってればわかるでしょ


（さきほど変化させたパラメーター「ＤＯＫＩ」を使って条件式ジャンプを発生させます）

（ＤＯＫＩが０であれば「わからん」のみ、１であれば「わかった」までが表示され、ＤＯＫＩが２以上であれば三つの選択肢が全て表示されます）

（条件式内の構文には、半角スペースは要りません）


&CJUMP ("わからん",D1,1,"わかった",D2,DOKI>=1,"はやくヤろーぜ",D3,DOKI>=2) ;




D1:
&SOUNDSTOP ;
&CG WHITE ;
&SE 01 ;
#1 うおぉぉーーーー！！！　わからねーーーーー！！！！　もうやめたーーーーー！！！

#雪奈 そういう場合はもう一度最初から読んでみましょうね

#瑞葉 どうせこいつの頭じゃ理解できないと思ってたわよ……

#1 もう…もう…オレには理解不能だっ！　しょせんオレはボンクラだよっ！！　うわぁぁぁぁぁん！！！！！

#瑞葉 もう、わかるまで先へは進めないわよ


&GOTO END ;




D2:
#1 よしっ、わかったぞ！

@MIZUHA_SUMASI ;
#瑞葉 本当かしら？

#1 ……なんとなくだけど

#雪奈 もう一度はじめからこの講座を読んでみるといいわよ。テキストだけを読むのもいいし、あわてる必要はないから

#1 ううっ、雪奈さんは優しいなあ。それに比べてこっちのおてんばは……

@MIZUHA_IKARI ;
#瑞葉 なーにーよーーー！　教えてもらっておいてその態度は何なのっ！！　こっちへ来なさいっ！！　性根を鍛え直してやるからっ！！

#1 いーーやーーーーー！！！！！

&CHARCLEAR MIZUHA ;

@YUKINA_SUMASI ;
#雪奈 あらら……

&GOTO EE ;





D3:
#1 ……ふぅ、オレにとってはコマンドの理解なんてどうでもいい！！

#1 だから〜、瑞葉〜〜、はやくヤることヤろーぜ〜〜

@YUKINA_MAKKA ;
#雪奈 まあ、やるって、何を？

@MIZUHA_MAKKA ;
#瑞葉 ば……っ、だ、だ、だ、ダメよーっ！！！

@YUKINA_NIKO ;
#雪奈 そうねえ、まだ教える事はあるし…

#1 えっ、まだあるの

@MIZUHA_SINKEN ;
#瑞葉 当たり前でしょ！

&GOTO EE ;




EE:
&CHARCLEAR ;
５・シナリオの移行について

（<FC YELLOW>『＆ＧＯＴＯＴＥＸＴ　シナリオファイル名』</FC>でそのシナリオのテキストに飛びます）

@MIZUHA_SUMASI ;
#瑞葉 シナリオファイルを、複数のテキストに分割した場合はこの命令を使うのよ

@YUKINA_SUMASI ;
#雪奈 同じように、<FC YELLOW>『＆ＪＵＭＰＴＥＸＴ　』</FC>や$E<FC YELLOW>『＆ＣＪＵＭＰＴＥＸＴ　』</FC>というのも使えるの。どれもラベルではなくテキストのファイル名を指定してあげてね

@MIZUHA_NIKO ;
#瑞葉 それと、ファイル名には全角文字は使用しない事！　以上！








&CHARCLEAR ;
６・ランダム分岐について



@MIZUHA_NIKO ;
#瑞葉 このコマンドはその名の通り、ランダムでストーリーを分岐させる命令なのよ！！

#瑞葉 使い方はとってもカンタン！　選択肢のジャンプコマンドと同じような書式で、分岐先のラベルを用意すればいいの！

@YUKINA_SUMASI ;
#雪奈 書式は、$E<FC YELLOW>『＆ＲＮＤＪＵＭＰ　（あ，い，う，え，お）』</FC>$Eと記述します

#雪奈 この場合、選択肢のように項目の文章は必要ないから

#1 つまり、ラベルのみでいいという事か

#瑞葉 まずは使って例示してみればいいわね

&FO ;
（この後、ランダムで分岐させて背景を変えます）

&RNDJUMP (R1,R2,R3)

R1:
&CG_BG MISEMAE ;
&GOTO FF ;

R2:
&CG_BG HIKAESITU ;
&GOTO FF ;

R3:
&CG_BG CORRETTO ;
&GOTO FF ;


FF:
#1 変わったのか…？

@MIZUHA_NIKO ;
#瑞葉 何度か試してみればわかるわよ

@YUKINA_SUMASI ;
#雪奈 このランダムについては、特に項目の上限は無いから、いくらでも増やして大丈夫よ

@YUKINA_NIKKORI ;
#雪奈 頑張って使いこなしてみてね

&BREAK ;
&SOUNDSTOP FO ;
&WAIT 6 ;










END:
&SOUND 10 ;
&CG CORRETTO ;

@MIZUHA_NIKO ;
#瑞葉 お疲れさま。選択肢やパラメーターについてはこのくらいね

@YUKINA_SUMASI ;
#雪奈 どう$1くん、選択肢の使い方は理解できたかしら？

#1 なんとなく……


@YUKINA_NIKKORI ;
#雪奈 これらの命令を使いこなして、楽しいお話を作ってね%00

@MIZUHA_NIKKORI ;
#瑞葉 目指せ明日のシナリオライター！　メジャーになれる日も遠くはないわよーー！！！！



&FO ;
&SOUNDSTOP FO ;
&WAIT 5 ;

<FC GREEN>『＆ＧＯＴＯＴＥＸＴ　』</FC>を使用してサンプルシナリオ冒頭に戻ります。終了する場合は、ＱＵＩＴを選んでください。

&GOTOTEXT SCENARIO ;

&END ;
