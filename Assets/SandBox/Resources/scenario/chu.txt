;
;
;
;　『瑞葉ちゃんのＭａｌｉｅシステム使用講座（中級編）』
;キャラ移動、文字命令、文章関連、サウンドループ、音声、ルビ
;体裁は紅のＡＳ講座に準拠
;
S:
&CG BLACK ;
&SOUND_CTRL LOOP OFF ;
&SOUND 2 ;
&CG_BG HIKAESITU ;
新フリーマリーでシナリオを作り始めること数日。

仕事が休みの日も、オレはシナリオ製作にのめり込んでいた。

@MIZUHA_NIKO SIFUKU C ;
#瑞葉 あらっ、頑張ってるみたいね

#1 瑞葉か……。面白いシナリオを作ろうしてみたんだが、今ひとつパッとしないみたいなんだ

@MIZUHA_NIKKORI2 ;
#瑞葉 しょうがないわねっ、ここはひとつ、私が高等テクニックを教えてあげるか

#1 お願いします。先生！

&FO ;

第二章　中級編　〜特殊コマンドを使いこなそう！〜


※まず、ゲーム画面をウィンドウモードに変更して下さい。

※『フリーマリーｖｅｒ４』のインストールフォルダ内の［¥ｄａｔａ¥ｓｃｅｎａｒｉｏ］フォルダ内にある［ｃｈｕ］というファイルを開いて、横に並べて下さい。

※このテキストを参照しながら、コマンドを覚えていって下さい

※それでは、Ａｒｅ　ｙｏｕ　ｒｅａｄｙ？












１・音声データを使ってみよう



&CG_BG HIKAESITU ;
（音声を再生するのには、『＆ＶＯＩＣＥ　音声ファイル名』、もしくは、セリフの頭に（ファイル名）を置くことによって鳴らすことができます）


（ファイルは［¥ｄａｔａ¥ｖｏｉｃｅ］フォルダ内に格納してください）

（それではサンプルのデータを使って音声再生の例をお見せします）

@MIZUHA_NIKKORI ;
#瑞葉 (mi1047)オーダー入ります。３番テーブルにアイス２お願いします！

@MIZUHA_SUMASI ;
#瑞葉 …と、こんな感じよ

#1 おおっ！　かっこいいじゃないか！

@MIZUHA_NIKKORI ;
(mi1125)
#瑞葉 お待たせしましたぁ。ランチのお客様は……


@MIZUHA_SUMASI ;
#瑞葉 半角括弧だけ置いても再生は出来るから、好きな方を使ってね

@MIZUHA_NIKKORI ;
&VOICE mi1797
#瑞葉 いらっしゃいませ、コレットにようこそ☆

@MIZUHA_SUMASI ;
#瑞葉 『＆ＶＯＩＣＥ　』を使った場合こんな感じね

#1 よしっ。メモメモ……

&CHARCLEAR ;











２・キャラクター移動と立ち位置指定について

@MIZUHA_SUMASI ;
#瑞葉 それから、立ちキャラクターを画面上で動かせるのは知っている？

#1 動かす……なんだそりゃ？


@MIZUHA_NIKKORI2 ;
#瑞葉 ふふふふふっ、その名も『＆ＣＨＡＲＭＯＶＥ　』よ！！


（『＆ＣＨＡＲＭＯＶＥ　』コマンドを使って、立ちキャラを移動させることができます）

@MIZUHA_NIKO C ;
#瑞葉 まず最初に普通に画面に立たせるでしょ

#瑞葉 次に$E『＆ＣＨＡＲＭＯＶＥ　キャラ名　移動させたい位置　加速設定　移動速度』の順で書式を打ち込むの

（加速設定は、１＝等速移動　２＝加速　３＝減速　となります）

（移動させたい位置は、Ｌ＝画面向かって左　Ｃ＝画面中央　Ｒ＝画面向かって右、）

（ＬＢ＝左奥　ＣＢ＝中央奥　ＲＢ＝右奥、ＬＯ＝画面外左側、ＲＯ＝画面外右側、ＬＢＯ＝画面外左奥、ＲＢＯ＝画面外右奥…の計１０箇所となっています）

@MIZUHA_NIKKORI2 ;
#瑞葉 これは立ち位置指定でも同じようにやれるのよ。それじゃ、移動してみるからね






;−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−

●立ち位置移動テストを行います。

（『＆ＣＨＡＲＭＯＶＥ』を使って、瑞葉が画面内を移動しますので、立ち位置・移動位置の参考にしてください）

;−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−

&CHARMOVE MIZUHA L 1 300 ;

（瑞葉が左に向かって等速で３００の速度で移動しました）


#瑞葉 ここからは移動実験ね

@MIZUHA_NIKO SIFUKU RB ;右奥
#瑞葉 まず右後ろに立つでしょ。次に中央の後ろに移動してみるわね

&CHARMOVE MIZUHA CB 3 800 ;中央奥
#瑞葉 ここが中央の後ろ。減速で８００の速さで移動したことになるのよ

&CHARMOVE MIZUHA LB 3 300 ;左奥
#瑞葉 で、こっちが左後ろになるわけ

&CHARMOVE MIZUHA LBO 3 300 ;画面外左奥
#1 画面からいなくなっちゃったぞ

&CHARMOVE MIZUHA LO 3 300 ;画面外左
#瑞葉 外側に移動するとこんな感じになるのよ

@MIZUHA_NIKKORI2 LO ;
&CHARMOVE MIZUHA L 1 300 ;
#瑞葉 それと、画面外でも表情を変える事はできるのよ

#1 なるほど……

（画面外から中へ移動させたい時は、あらかじめ画面外に立たせてから『＆ＣＨＡＲＭＯＶＥ』を使います）

@MIZUHA_SUMASI ;
#瑞葉 あとは右方向に素早く動いていくわね

&CHARMOVE MIZUHA C 2 200 ;中
&WAIT 1 ;
&CHARMOVE MIZUHA R 2 200 ;右
&WAIT 1 ;
&CHARMOVE MIZUHA RO 2 200 ;画面外右

（……このように画面内を移動させることができます）


#瑞葉 カンタンに言うと、２だと速く、３だと遅くなるってこと。見た目ではそんなに気になる事でもないから、大体の目安として覚えておけばいいわ

#1 なるほど……これを使いこなすとかなり面白いな



@YUKINA_SUMASI SIFUKU RO ;
&CHARMOVE YUKINA R 1 500 ;
#雪奈 ねえ、瑞葉ちゃん、暇だったらお買い物に行ってきて欲しいんだけど

@MIZUHA_MAKKA LO ;
&CHARMOVE MIZUHA L ;
#瑞葉 ふう……。うん、いいけど……

@YUKINA_NIKO ;
#雪奈 あらっ、な〜に〜？　息切らせちゃって。二人してお楽しみの最中だったのかしら？

@MIZUHA_PUN ;
#瑞葉 ち、違うわよ！

#雪奈 ふ〜〜ん、まあいいけど

@MIZUHA_SINKEN ;
#瑞葉 ほらっ、とっとと行くわよ$1！

&CHARMOVE MIZUHA LO 1 300 ;

#1 しょうがない。行くか……

@YUKINA_SUMASI ;
#雪奈 待って、$1くんはこれからお勉強よ

#1 え〜〜〜！！

&FO ;
&WAIT 4 ;







３・文字演出について


;■文字サイズ
&CG_BG HIKAESITU ;
@YUKINA_NIKO C ;
#雪奈 初級講座までの話で基本は理解できたと思うから、今度はもう少し難しい技について説明するわね

#雪奈 文字を大きくしたり、色を変えたりできるのよ

#1 なんと、それは凄い！

@YUKINA_NIKKORI2 ;
#雪奈 では、さっそく具体例を見ながら覚えていきましょう

#雪奈 文字の大きさを変更する命令は、$e『＆ＦＯＮＴＳＩＺＥ　［数値］』と書きます

&FONTSIZE 35 ;
#雪奈 サイズ３５で指定してみたわよ

#1 これは大きすぎますって

&FONTSIZE 6 ;
#雪奈 サイズ６にするとこういう感じね

#1 小さすぎて読めないんですけど…

&FONTSIZE NORMAL ;
@YUKINA_NIKO ;
#雪奈 元の大きさに戻すには、数値の所に”ＮＯＲＭＡＬ”と記述すれば良いの

#1 なるほどなるほど

#雪奈 文字を大きくしすぎると、画面に入りきらない事があるのから注意してね


;■文字色
@YUKINA_SUMASI ;
#雪奈 今度は文字の色を変える命令ね

#1 色を変える…？

&FONTCOLOR YELLOW ;
（『＆ＦＯＮＴＣＯＬＯＲ　［色指定］』で、表示される文章のフォント色を変更できます）

@YUKINA_SUMASI ;
#雪奈 この命令を一度使うと、以降の文章すべての色が変わるの


&FONTCOLOR WHITE ;
#雪奈 元に戻したい場合は、基本色である［ＷＨＩＴＥ］で指定し直す事。覚えておいてね

#1 使える色はどんなのがあるんですか？


@YUKINA_NIKO ;
#雪奈 色名の指定で使えるものは、$EＷＨＩＴＥ、ＢＬＡＣＫ、ＲＥＤ、ＢＬＵＥ、ＧＲＥＥＮ、ＹＥＬＬＯＷ…の６色があって…

#雪奈 これら以外の色…たとえばピンク色や紫色を使いたい場合は、ＷＥＢセーフカラーでの指定を行うの

#1 セーフカラー？

@YUKINA_SUMASI ;
#雪奈 いわゆるＨＴＭＬ方式の”＃○○▼▼＊＊”とかいう指定方法ね。これだとさらに細かく色指定が行えるのよ

&FO ;
&FONTCOLOR "#FF69B4" ;
#雪奈 ”＃ＦＦ６９Ｂ４”だと、だいたいこんな感じにピンク色っぽくなって…

&FONTCOLOR "#FFA500" ;
#雪奈 ”＃ＦＦＡ５００”でオレンジ色

&FONTCOLOR "#FFFFFF" ;WHITEと同じ
#雪奈 この指定方法を使う場合、必ず半角の”（ダブルクオーツ）で挟んでね

#1 なるほど〜


また、ＨＴＭＬタグのように、＜ＦＳ＞〜＜／ＦＳ＞書式で、文章内の文字サイズを部分的に変更することができます。

同様に、＜ＦＣ＞〜＜／ＦＣ＞書式で、文章内の一部の色を変更することができます。



それでは、<fc red>簡単に使用例</fc>をお見せします。$E<FS 25>文字の表示</FS>が変化しているのがわかりましたか？


これらの命令は、<FS 30><fc yellow>部分的に強調したい時</fc></FS>に便利です。また、複数の書式を重ねて使うことも<fc "#ffc0cb"><FS 28>でき</FS><FS 12>ます</FS>。</FC>

"</FS>"や"</FC>"で閉じないと、それ以降の文章が<FS 28>全て<FC GREEN>変化した状態になってしまいます。

&FONTSIZE NORMAL ;
いったん初期設定に戻します。基本サイズは２０です。





;■ルビ付き文字
&CG HIKAESITU ;
@YUKINA_SUMASI ;
#雪奈 そうそう。今回新しくルビ付き命令が使えるようになったの

#1 ルビ？

#雪奈 いわゆる”振り仮名”付きの文字の事ね

#1 それは凄いですね〜

#雪奈 使い方は、$E＜ＲＢ　”フリガナ”＞［文字列］＜／ＲＢ＞と書きます


#雪奈 <RB "ユキナ">雪奈</RB>とか<RB "くれない">紅</RB>とか、<RB "ぼくとぼくらのなつ">僕と、僕らの夏</RB>のように表示させる事ができるのよ

#1 <RB "ふじのみや　みずは">藤之宮　瑞葉</RB>……<RB "イチムラ　キリ">市村　貴理</RB>……なるほど、こんな感じか

@YUKINA_NIKKORI ;
#雪奈 はい、よくできました☆




;■表示位置
@YUKINA_SUMASI ;
#雪奈 今度は文章の表示位置について説明するわね

&CHARCLEAR ;

<FC YELLOW>「＆ＦＯＮＴＰＯＳＩＴＩＯＮ　（位置）」</FC>で、表示される文章の位置を指定できます。

指定位置は全部で九つあります。

&FONTPOSITION LEFT_TOP ;
左上（初期設定）

&FONTPOSITION LEFT_CENTER ;
左中段

&FONTPOSITION LEFT_DOWN ;
左下

&FONTPOSITION CENTER_TOP ;
中央上

&FONTPOSITION CENTER ;
中央

&FONTPOSITION CENTER_DOWN ;
中央下

&FONTPOSITION RIGHT_TOP ;
右上

&FONTPOSITION RIGHT_CENTER ;
右中段

&FONTPOSITION RIGHT_DOWN ;
右下

&FONTPOSITION LEFT_TOP ;
…となっています。文字の大きさに注意して使用してください。

@YUKINA_NIKO ;
#雪奈 基準は『ＬＥＦＴ＿ＴＯＰ』だから、ここに戻すようにすると良いわよ


;■応用
#雪奈 この命令を簡略化したものが、"<C>・<R>・<L>"で…

#雪奈 <C>"<C>"をセリフの頭に置くとこんな具合に表示されるの。$K$E"<R>"が右寄せ、"<L>"が左寄せね

#1 <R>右寄せだと…なるほどこうか

#雪奈 <L>左寄せだと、まあ、いつもの感じね

#雪奈 <R>複数を<C>組み合わせて使うと<L>こういうふうに出るのよ

<C>この場合、<R>二つ目からは<L>自動で改行されます。

&FO ;








４・文章表示について


;■テキストスピード
&CG_BG HIKAESITU ;
@YUKINA_NIKO ;
#雪奈 <FC YELLOW>『＆ＴＥＸＴＳＰＥＥＤ　（数値）』</FC>の記述でテキストの表示速度の変更が行えるのよ

&CHARCLEAR ;
&TEXTSPEED 100 ;
数値が１で<FC "#800080">１ミリ秒</FC>の速度になります。（この場合０．１秒で一文字表示）

&TEXTSPEED 1000 ;
数値１０００で、一文字表示されるのに<FC "#800080">１秒</FC>かかる計算になります。



&TEXTSPEED 0 ;

数値が少ないと速くなり、多くすると遅くなる仕組みです。

数値０で一度に全ての文章が表示されます。

&TEXTSPEED NORMAL ;

"&TEXTSPEED NORMAL"で元の速度（コンフィグで設定した速度）に戻します。

#1 なるほど〜





;■テキストの自動送り

&TEXTMODE SYNC ;
@YUKINA_NIKO ;
#雪奈 <FC YELLOW>『＆ＴＥＸＴＭＯＤＥ　<FC "#FFA500">ＳＹＮＣ</FC>』</FC>と記述することで文章の自動送りを行えます
&WAIT 8 ;

#1 ははあ、今がちょうど自動送り中なんですね
&WAIT 6 ;

&CHARCLEAR ;
<FC RED>※注意</FC>　"&TEXTMODE SYNC"の際は、<FC GREEN>"&WAIT"</FC>は文章の上ではなくて下に配置すること！
&WAIT 8 ;

また、間の空改行は抜かしてしまっても構いません。
&WAIT 8 ;
文章自動送りは特別な演出をしたい時に向いています。
&WAIT 8 ;




&TEXTMODE NORMAL ;
@YUKINA_SUMASI ;
#雪奈 元に戻す場合は、<FC YELLOW>『＆ＴＥＸＴＭＯＤＥ　<FC "#FFA500">ＮＯＲＭＡＬ</FC>』</FC>と記述してね

#1 自動送りが"SYNC"で、通常は"NORMAL"ですね。メモメモ……っと

&FO ;



５・ＢＧＭ再生を一度だけ行う

;■サウンドのループ機能
&CG_BG HIKAESITU ;
@YUKINA_SUMASI ;
#雪奈 そろそろＢＧＭが止まっている頃だと思うんだけど……わかる？

#1 むぅ…そういえば静かだな

@YUKINA_NIKO ;
#雪奈 このお話の冒頭でＢＧＭのループを止めているからなのよ

@YUKINA_SUMASI ;
#雪奈 通常だと、ＢＧＭはループ再生が行われていて、同じ曲が連続して流れるようになっているんだけど…

#雪奈 そのループを意図的に止める事が出来るの

#雪奈 その書式は、$E『＆ＳＯＵＮＤ＿ＣＴＲＬ　ＬＯＯＰ　<FC YELLOW>ＯＦＦ</FC>』と打ち込むだけ

#雪奈 これで一度曲が終わると、もう一度"&SOUND"の指定をしない限り鳴り出す事は無くなるわ

#1 ふ〜〜む

@YUKINA_NIKO ;
#雪奈 元に戻したい場合は、『＆ＳＯＵＮＤ＿ＣＴＲＬ　ＬＯＯＰ　<FC YELLOW>ＯＮ</FC>』と記述してね

&CHARCLEAR ;
&SOUND_CTRL LOOP ON ;
（ＢＧＭのループをＯＮにします）

&SOUND 10 ;
（この命令だけでＢＧＭが鳴り始めるわけではありませんので、停止状態から再生する場合は、再び"&SOUND"を使ってください）






@YUKINA_NIKKORI2 C ;
#雪奈 今日はこんな所かしらね。お疲れさまでした

#1 いやぁ〜〜奥が深いですねぇ〜〜

@YUKINA_NIKO ;
#雪奈 ここまでの講座で、一本道のストーリーは作れると思うから、頑張ってみてね

#1 はいっ、頑張ります！！

&SOUNDSTOP FO ;
&FO ;
&WAIT 6 ;
『＆ＧＯＴＯＴＥＸＴ　』命令を使用してサンプルシナリオ冒頭に戻ります。終了する場合は、ＱＵＩＴを選んでください。

&GOTOTEXT SCENARIO ;


&END ;


